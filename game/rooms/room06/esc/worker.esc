:look
say player "He's wearing a badge that says "Careers Officer"."

####################################################################################################
:pickup
say player "I don't think he'd like that."

####################################################################################################
:talk
> [!talked_once]
	turn_to worker player
	say player "Hi."
	say worker "Have you come about the job?"
	set_global talked_once true

turn_to worker player
?
	- "What job are you hiring for?"
		say player 	"What job are you hiring for?"
		say worker 	"We're recruiting adventurers!"
		?
		- "Why would I want to be an adventurer?"
			say player	"Why would I want to be an adventurer?"
			say worker	"Fame!"
			say worker	"Adventure!"
			say worker	"Almost certain death!"
		- "How do I apply?"
			say player	"How do I apply?"
			say worker	"All you need to do is find an ..."
			say worker	"adventurer application form and fill it in"
		- "Do I need my own sword?" [!hide_this_option]
			say player	"If there's a dragon, do I need my own sword?"
			say worker	"Can you run fast?"
			say player	"Not really."
			say worker	"Then yes."
			set_global hide_this_option true
		- "I've got to go."
			say player	"I've got to go."
			say worker	"Bye!"
			turn_to worker worker_face_down
			stop
		!
	- "No, I'm not looking for a job."
		say player	"No, I'm not looking for a job."
		say worker 	"Well you know where to find me if you change your mind."
		turn_to worker worker_face_down
		stop
!

####################################################################################################
# accept both use or give commands to progress the game
:give r5_filled_sheet
set_gui_visible false
accept_input SKIP

inventory_remove r5_filled_sheet
say worker "I've unlocked the door, in you go!"
walk worker r6_r_exit

# Open the door
set_global r6_r_exit_locked false
# Hide the closed door graphic
set_state r6_r_exit open_door
# Make the open exit visible
set_active r6_r_exit true

# Disappear!
say worker "I will see you again... MUAHAHAHAHA!"
wait 1
set_active worker false

set_gui_visible true
accept_input ALL


